//------------------------------------------------
//      File: FFXIV_EXH
//   Authors: Zee
//   Version: 1.0
//   Purpose: FFXIV Database Header
//  Category: FFXIV
//------------------------------------------------

#include "Lib/Common.bt"
BigEndian();

enum<byte> ExcelVariant {
    Unknown = 0,
    Default = 1,
    SubRows = 2,
};

struct Header
{
    char magic[4];
    ushort version;
    ushort dataOffset;
    ushort columnCount;
    ushort pageCount;
    ushort languageCount;
    ushort unk0;
    byte unk1;
    ExcelVariant variant;
    ushort unk2;
    int rowCount;
    uint unk3;
    uint unk4;
};

enum<ushort> ExcelColumnDataType {
    String = 0x0,
    Bool = 0x1,
    Int8 = 0x2,
    UInt8 = 0x3,
    Int16 = 0x4,
    UInt16 = 0x5,
    Int32 = 0x6,
    UInt32 = 0x7,
    Float32 = 0x9,
    Int64 = 0xA,
    UInt64 = 0xB,

    // 0 is read like data & 1, 1 is like data & 2, 2 = data & 4, etc...
    PackedBool0 = 0x19,
    PackedBool1 = 0x1A,
    PackedBool2 = 0x1B,
    PackedBool3 = 0x1C,
    PackedBool4 = 0x1D,
    PackedBool5 = 0x1E,
    PackedBool6 = 0x1F,
    PackedBool7 = 0x20,
};

enum<ushort> Language {
    None,
    // ja
    Japanese,
    // en
    English,
    // de
    German,
    // fr
    French,
    // chs
    ChineseSimplified,
    // cht
    ChineseTraditional,
    // ko
    Korean
};

struct Column
{
    ExcelColumnDataType dataType;
    short dataOffset;
};

struct Page
{
    uint startId;
    uint rowCount;
};

Header header;
Column columns[header.columnCount];
Page pages[header.pageCount];
Language languages[header.languageCount];